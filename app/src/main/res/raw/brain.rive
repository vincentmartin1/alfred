// + : trigger operator
// - : reply operator
// % : previous operator

// # in a trigger : matches numbers only
// _ in a trigger : matches single-word only
// * in a trigger : matches anything

! version = 2.0

// Begin block
> begin
    + request
    - {ok}
< begin

// Substitutions
// ! sub ... = ...

// Variable definitions
! var master = localuser
! var botname = Alfred

+ envoie un message a *
- <set dst=<star>>
^ Que voulez-vous envoyer à <get dst> ?

+ *
% Que voulez-vous envoyer à <get dst> ?
- CMD;SEND;<get dst>;<star2>

// Basic answer
+ *
- REP;What do you mean?
- REP;Try asking your question a different way.

// Debug replies : set variable and get value
+ set _ *
- REP;<set <star1>=<star2>>
^ <star1> set to <star2>.

+ get *
- REP;<star> is : <get <star>>

// Tests
+ how are you
- i am fine and you

+ i am fine too
% i am fine and you
- cool we are both fine

+ my _ is *
- <set <star1>=<star2>>I will remember that your <star1> is <star2>.

+ what is my _
* <get <star>> == undefined => I do not know your <star>.
- REP;Your <star> is <get <star>>

// Replies
+ hello bot
- REP;Hello, human!

+ (my name is|i am) *
- REP;<set name=<star2>>I will remember it, <get name>.
- REP;<set name=<star2>>Nice to meet you, <get name> !

+ (who am i|what is my name)
* <get name> == undefined => I do not know your name. What is your name ?
- REP;You are <get name>.
- REP;Your name is <get name>.

+ (what is your name|who are you)
- REP;My name is <get botname>

// ------ Réponses en français ------

// Command:OPEN
//   Ouvre une application
+ ouvre *
- CMD;OPEN;<star>

// Command:TEXT
//   Envoie un SMS
+ envoie * à * par sms
- CMD;TEXT;<star2>;<star1>

// Command:MAIL
//   Envoie un mail
+ envoie * à * par mail
- CMD;MAIL;<star2>;<star1>

// Command:CALL
//   Appelle un contact
+ appelle *
- CMD;CALL;<star>

// Command:WEB
//   Effectue une recherche sur le Web
+ cherche * sur google
- CMD;WEB;<star>

+ google *
- CMD;WEB;<star>

// Command:DATE
//   Renvoie la date actuelle
+ (quelle est la date|quel jour sommes nous)
- CMD;DATE

// Command:WEATHER
//   Renvoie la météo à un certain endroit
+ (quel temps fait il|quelle est la météo) à *
- CMD;WEATHER;<star2>

+ quel temps fait il (ici)
- CMD;WEATHER

// Command:POSITION
//   Renvoie la localisation
+ (où suis je|quelle est ma position|quelle est ma localisation)
- CMD;POSITION

// Command:EVENT
//  Ajoute un évènement au calendrier
+ ajoute l'évènement * au calendrier à la date du *
- CMD;EVENT;<star1>;<star2>

// Command:REMINDER
//   Ajoute un rappel
+ rappelle moi de *
- CMD;REMINDER;<star>

// Command:MATH
//   Effectue un calcul
+ calcule # _ #
- CMD;MATH;<star1>;<star2>;<star3>
